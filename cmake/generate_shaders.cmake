function(embed_shader shader_file shader_name output_variable)
    file(READ "${shader_file}" shader_bytes HEX)
    string(REGEX REPLACE "([0-9a-f][0-9a-f])" "0x\\1," shader_bytes_cpp "${shader_bytes}")
    string(REGEX REPLACE ",$" "" shader_bytes_cpp "${shader_bytes_cpp}")

    set(${output_variable} "${shader_bytes_cpp}" PARENT_SCOPE)
endfunction()

if(NOT OUTPUT)
    message(FATAL_ERROR "OUTPUT variable not set")
endif()

if(NOT SPIRV_BINARIES)
    message(FATAL_ERROR "SPIRV_BINARIES variable not set")
endif()

set(CPP_CONTENT "// Generated by generate_shaders.cmake\n")
set(CPP_CONTENT "${CPP_CONTENT}// NOLINTBEGIN\n\n")
set(CPP_CONTENT "${CPP_CONTENT}#include <vk/Shaders.hpp>\n\n")
set(CPP_CONTENT "${CPP_CONTENT}namespace polaris::vk {\n\n")
set(CPP_CONTENT "${CPP_CONTENT}namespace {\n\n")

set(SHADER_DECLARATIONS "")
set(SHADER_MAP_ENTRIES "")

string(REPLACE " " ";" SPIRV_BINARIES "${SPIRV_BINARIES}")
foreach(SHADER_SPV ${SPIRV_BINARIES})
    file(RELATIVE_PATH SHADER_RELATIVE "${SPIRV_OUT_DIR}" "${SHADER_SPV}")
    string(REGEX REPLACE "\\.spv$" "" SHADER_NAME "${SHADER_RELATIVE}")
    string(MAKE_C_IDENTIFIER "${SHADER_NAME}" SHADER_IDENTIFIER)

    embed_shader("${SHADER_SPV}" "${SHADER_IDENTIFIER}" SHADER_BYTES)
    set(SHADER_DECLARATIONS "${SHADER_DECLARATIONS}alignas(4) inline constexpr auto shader_${SHADER_IDENTIFIER} = std::to_array<uint8_t>({ ${SHADER_BYTES} });\n")
    set(SHADER_MAP_ENTRIES "${SHADER_MAP_ENTRIES}    ShaderInfo{\"${SHADER_NAME}\", std::span{shader_${SHADER_IDENTIFIER}}},\n")
endforeach()

set(CPP_CONTENT "${CPP_CONTENT}${SHADER_DECLARATIONS}\n")
set(CPP_CONTENT "${CPP_CONTENT}consteval auto make_shaders_array() {\n")
set(CPP_CONTENT "${CPP_CONTENT}  return std::to_array({\n")

set(CPP_CONTENT "${CPP_CONTENT}${SHADER_MAP_ENTRIES}")
set(CPP_CONTENT "${CPP_CONTENT}  });\n")
set(CPP_CONTENT "${CPP_CONTENT}}\n\n")
set(CPP_CONTENT "${CPP_CONTENT}inline constexpr auto shaders_array = make_shaders_array();\n\n")
set(CPP_CONTENT "${CPP_CONTENT}}  // namespace\n\n")
set(CPP_CONTENT "${CPP_CONTENT}std::span<const ShaderInfo> ShaderManager::shaders() noexcept {\n")
set(CPP_CONTENT "${CPP_CONTENT}  return shaders_array;\n")
set(CPP_CONTENT "${CPP_CONTENT}}\n\n")
set(CPP_CONTENT "${CPP_CONTENT}}  // namespace polaris::vk\n")
set(CPP_CONTENT "${CPP_CONTENT}// NOLINTEND\n")

file(WRITE "${OUTPUT}" "${CPP_CONTENT}")
